TITLE:: MPadView
summary:: a view for noteOn/noteOff/touch elements
categories:: GUI
related:: Overviews/Modality, Classes/MKtl, Classes/MKtlElement

DESCRIPTION::
MPadView is used in MKtlGUIs to automatically generate representations of pads and piano keys as commonly used in MIDI devices. You may never need to use MPadView explicitly.

It flexibly supports a variety of behaviors these noteOn, noteOff and touch elements may have, discussed here for clarity, and to possibly add more arcane combinations when encountered.

These types of MIDI elements include:

table::
	## tag: || description: || noteOn: || noteOff: || touch:
	## || - DEFAULT - ||
	## \noteOnOff || piano key || 1-127 || 0 || off
	## || - COMMON - ||
	## \noteOnOffBut || basic button || 127 || 0 || off
	## \noteOnTrig || trigger pad || 127 || off || off
	## \noteOnVel || trig pad w/ velocity || 1-127 || 0 || off
	## \noteOnOffTouch || piano key w/ touch || 1-127 || 0 || 0-127
	## || - RARE - ||
	## \noteOnOffVel || piano key || 1-127 || 1-127 || off
	## \noteOnOffVelTouch || piano key || 1-127 || 1-127 || 1-127
::

Subsection::Code Examples for demonstration:

code::
// make an MPadView, and show its configurations/behavior
m = MPadView().front;
// set its label
m.label_("noteOnOff");
// set its basic actions for mouseDown, mouseUp and mouseMove
m.action = { |mp| "noteOn vel: %\n".postf(mp.value) };
m.upAction = { |mp| "noteOff vel: %\n".postf(mp.upValue) };
m.moveAction = { |mp| "touch value: %\n".postf(mp.moveValue) };

// -> try its default behavior:
// y-pos on pad determines noteOn velocity, noteOff is 0, touch is off
[m.useValue, m.useUpValue, m.useMoveValue ];

// noteOn/off button only - turn off useValue
m.label_("noteOnOffBut");
m.useValue = false; m.useUpValue = false; m.useMoveValue = false;

// use noteOn, e.g. for simple trigger pads:
m.label_("noteOn");
m.autoUpTime = 0.1; // hold value, then velocity falls by itself
m.useValue = false; m.useUpValue = true; m.useMoveValue = false;

// use only noteOn with velocity, e.g. for drum pads:
m.label_("noteOnVel");
m.autoUpTime = 0.1; // velocity falls by itself
m.useValue = true; m.useUpValue = true; m.useMoveValue = false;


// useMouseMove to enable polytouch sensing on key
m.label_("noteOnOffTouch");
m.autoUpTime = inf;
m.useMoveValue = true;
m.moveAction = { |mp| "touch: %\n".postf(mp.moveValue) };


// noteOff : enable velocity
m.label_("noteOnOffVel");
m.autoUpTime = inf;
m.useValue = true; m.useUpValue = true; m.useMoveValue = false;

m.baseColor = Color.black(0.1);

::

CLASSMETHODS::

METHOD:: new
argument:: parent
argument:: bounds


INSTANCEMETHODS::

private:: init, stopTasks

METHOD:: baseColor
get and set mpadview's background color
METHOD:: hiliteColor
get and set color for when view is activated

METHOD:: value, valueAction
get and set current value (and optionally do its action): Clicking on gui sets it to y position within view. Usually used as noteOn velocity.

METHOD:: upValue, upValueAction
get and set current upValue (and optionally do its action): releasing mouseClick on gui sets it to y position within view. Usually used for velocity decay; also for noteOff velocity.

METHOD:: moveValue, moveValueAction
get and set current moveValue (and optionally do its action): moving the held mouse on gui sets it to y position within view. Usually used for polytouch value.

METHOD:: useValue, useUpValue, useMoveValue
get and set booleans whether to use value, upValue, moveValue.
This supports different behavior modes.

METHOD:: action, upAction, moveAction
get and set actions to do when value, upValue, moveValue changes

METHOD:: doAction, doUpAction, doMoveAction
evaluate action, upAction moveAction.

METHOD:: pressed
get and set boolean whether pad is pressed.

METHOD:: autoUpTime
get and set time for automatic upAction after activation.
default inf means it waits for release of mouseclick,
short times (e.g. 0.1) allow trigger pad-like behavior.


METHOD:: label
get and set view label to show
METHOD:: vShiftLabel
shift label by vertical offset
METHOD:: font
get and set font to use for label
